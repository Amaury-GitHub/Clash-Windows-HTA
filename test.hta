<head>
<title>Clash Control Panel</title>
<HTA:APPLICATION
    applicationName="Clash Control Panel"
    border="Dialog"
    contextMenu="No"
    icon="#"
    maximizeButton="No"
    selection="No"
    scroll="No"
    singleInstance="Yes"
/>
</head>

<script language="VBScript">

    Set objWMIService=GetObject("winmgmts:\\.\root\cimv2")
    Set objReg=GetObject("winmgmts:{impersonationLevel=impersonate}!\\.\root\default:StdRegProv")
    Set objShell=CreateObject("WScript.Shell")
    Dim objTimer

    Sub Window_onLoad
        Window.ResizeTo 512,678
        GetVersion
        GetStatus
        objTimer=setInterval("GetStatus", 1000)
    End Sub

    Sub GetVersion
        objShell.Run "cmd /c clash-windows-amd64 -v | clip", 0, true
        ClashStatus.InnerHTML="Clash" & "<br>" & "Version " & Mid (CreateObject("htmlfile").ParentWindow.ClipboardData.GetData("text"), 7, 11)
    End Sub

    Sub GetStatus
        Set colClashStatus=objWMIService.ExecQuery ("Select * from Win32_Process Where Name='clash-windows-amd64.exe'")
        objReg.GetDWORDValue &H80000001, "Software\Microsoft\Windows\CurrentVersion\Internet Settings", "ProxyEnable", colProxyStatus
        If colClashStatus.Count > 0 Then
            ClashStatus.style.backgroundColor="green"
        Else
            ClashStatus.style.backgroundColor="red"
        End If
        If colProxyStatus > 0 Then
            ProxyStatus.style.backgroundColor="green"
        Else
            ProxyStatus.style.backgroundColor="red"
        End If
        If colClashStatus.Count > 0 and colProxyStatus > 0 Then
            GuideArea.InnerHTML="Clash & 系统代理 已打开"
        ElseIf colClashStatus.Count = 0 and colProxyStatus = 0 Then
            GuideArea.InnerHTML="Clash & 系统代理 未打开"
        ElseIf colClashStatus.Count = 0 and colProxyStatus > 0 Then
            GuideArea.InnerHTML="Clash 未打开"
        ElseIf colClashStatus.Count > 0 and colProxyStatus = 0 Then
            GuideArea.InnerHTML="系统代理 未打开"
        End If
    End Sub

    Sub StartClash
        clearInterval(objTimer)
        GuideArea.InnerHTML="关闭 Clash"
        objShell.Run "taskkill /im clash-windows-amd64.exe /t /f", 0, True
        GuideArea.InnerHTML="关闭系统代理"
        objShell.Run "reg add ""HKCU\Software\Microsoft\Windows\CurrentVersion\Internet Settings"" /v ProxyEnable /t REG_DWORD /d 0 /f", 0
        objShell.Run "reg delete ""HKCU\Software\Microsoft\Windows\CurrentVersion\Internet Settings\ProxyOverride""", 0
        objShell.Run "reg delete ""HKCU\Software\Microsoft\Windows\CurrentVersion\Internet Settings\ProxyServer""", 0
        GuideArea.InnerHTML="更新 Country.mmdb"
        objShell.Run "powershell (new-object System.Net.WebClient).DownloadFile('https://cdn.jsdelivr.net/gh/Hackl0us/GeoIP2-CN@release/Country.mmdb','Country.mmdb')", 0, True
        GuideArea.InnerHTML="打开系统代理"
        objShell.Run "reg add ""HKCU\Software\Microsoft\Windows\CurrentVersion\Internet Settings"" /v ProxyEnable /t REG_DWORD /d 1 /f", 0
        objShell.Run "reg add ""HKCU\Software\Microsoft\Windows\CurrentVersion\Internet Settings"" /v ProxyOverride /t REG_SZ /d <local> /f", 0
        objShell.Run "reg add ""HKCU\Software\Microsoft\Windows\CurrentVersion\Internet Settings"" /v ProxyServer /t REG_SZ /d 127.0.0.1:7890 /f", 0
        GuideArea.InnerHTML="打开 Clash"
        objShell.Run "clash-windows-amd64 -d . -f .\Amaury-Clash.yaml", 0
        GuideArea.InnerHTML="打开 Clash & 系统代理 完成"
        objTimer=setInterval("GetStatus", 1000)
    End Sub

    Sub StopClash
        clearInterval(objTimer)
        GuideArea.InnerHTML="关闭 Clash"
        objShell.Run "taskkill /im clash-windows-amd64.exe /t /f", 0, True
        GuideArea.InnerHTML="关闭系统代理"
        objShell.Run "reg add ""HKCU\Software\Microsoft\Windows\CurrentVersion\Internet Settings"" /v ProxyEnable /t REG_DWORD /d 0 /f", 0, true
        objShell.Run "reg delete ""HKCU\Software\Microsoft\Windows\CurrentVersion\Internet Settings"" /v ProxyOverride /f", 0
        objShell.Run "reg delete ""HKCU\Software\Microsoft\Windows\CurrentVersion\Internet Settings"" /v ProxyServer /f", 0
        GuideArea.InnerHTML="关闭 Clash & 系统代理 完成"
        objTimer=setInterval("GetStatus", 1000)
    End Sub

    Sub OpenRazord
        objShell.Run "http://clash.razord.top/"
    End Sub

    Sub OpenYacd
        objShell.Run "http://yacd.haishan.me/"
    End Sub

    Sub StartEnableLoopback
        objShell.Run "EnableLoopback.exe"
    End Sub

    Sub OpenClash
        objShell.Run "https://github.com/Dreamacro/clash/releases/tag/premium"
    End Sub

</script>

<body>
<table border="8" cellspacing="8" width="100%">
    <tr>
        <th colspan="2" align="center"><font size=6>Clash Control Panel</font></th>
    </tr>
    <tr height="100px">
        <th width="50%" id="ClashStatus"><font size=4>Clash</font></th>
        <th id="ProxyStatus"><font size=4>系统代理</font></th>
    </tr>
    <tr>
        <td colspan="2" id="GuideArea" align="center">&nbsp;</td>
    </tr>
    <tr height="100px">
        <td align="center"><button onclick="StartClash" style="width:160px;height:80px">打开<br>Clash</button></td>
        <td align="center"><button onclick="StopClash" style="width:160px;height:80px">关闭<br>Clash</button></td>
    </tr>
    <tr height="100px">
        <td align="center"><button onclick="OpenRazord" style="width:160px;height:80px">Razord<br>Dashboard</button></td>
        <td align="center"><button onclick="OpenYacd" style="width:160px;height:80px">Yacd<br>Dashboard</button></td>
    </tr>
    <tr height="100px">
        <td align="center"><button onclick="StartEnableLoopback" style="width:160px;height:80px">运行<br>EnableLoopback</button></td>
        <td align="center"><button onclick="OpenClash" style="width:160px;height:80px">更新<br>Clash 核心</button></td>
    </tr>
    <tr>
        <td colspan="2" align="right"><i>Designed by Amaury</i></td>
    </tr>
</table>
</body>